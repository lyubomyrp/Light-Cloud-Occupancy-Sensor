;******************************************************************************
;  imul16.s43 (IAR version) - 16 bit multiply
;
;  Copyright (C) 2011 Texas Instruments Incorporated - http://www.ti.com/ 
; 
;  Redistribution and use in source and binary forms, with or without 
;  modification, are permitted provided that the following conditions 
;  are met:
;
;    Redistributions of source code must retain the above copyright 
;    notice, this list of conditions and the following disclaimer.
;
;    Redistributions in binary form must reproduce the above copyright
;    notice, this list of conditions and the following disclaimer in the 
;    documentation and/or other materials provided with the   
;    distribution.
;
;    Neither the name of Texas Instruments Incorporated nor the names of
;    its contributors may be used to endorse or promote products derived
;    from this software without specific prior written permission.
;
;  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS 
;  "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT 
;  LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
;  A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT 
;  OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, 
;  SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT 
;  LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
;  DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
;  THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT 
;  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE 
;  OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;
;******************************************************************************

#include "io.h"
#include "macros.m43"

;This routine implements a 16x16->32 2's complement multiplier. If a hardware
;multiplier is available it is used. If no hardware multiplier is available,
;Booth's algorithm is used to directly implement a signed multiplier in software.
;C does not normally express a 16x16->32 operation, and the speedup of achieving
;a 32-bit answer by a full 32x32 multiply is substantial.

#if !defined(__IAR_SYSTEMS_ASM__)  ||  !(((__TID__ >> 8) & 0x7f) == 43)
#error This file is compatible with the IAR MSP430 assembler.
#endif

#if __VER__ < 400
#error This file is compatible with the IAR MSP430 assembler 4.0 or later.
#endif

; Parameters
#define x               R12
#define y               R13

; Result
#define ret_lo          R12
#define ret_hi          R13

;int32_t imul16(int16_t x, int16_t y)
    public imul16

    RSEG CODE
imul16
#if (defined(__MSP430_HAS_MPY__)  ||  defined(__MSP430_HAS_MPY32__))  &&  !defined(__TOOLKIT_USE_SOFT_MPY__)
    push.w  SR
    dint
    mov.w   x,&MPYS
    mov.w   y,&OP2
    mov.w   &RESHI,ret_hi
    mov.w   &RESLO,ret_lo
    pop.w   SR
#else
    push.w  R10
    push.w  R15
    mov.w   x,R15
#if __VER__ >= 400
    mov.w   y,R14
#endif
    clr.w   R13
    mov.w   R13,x

    mov.w   R13,R10
    tst.w   R15
    jge     booth_2
    mov.w   #-1,R10
    jmp     booth_2

booth_6
    add.w   R15,x
    addc.w  R10,R13
booth_1
    rla.w   R15
    rlc.w   R10
booth_2
    rra.w   R14
    jc      booth_5
    jne     booth_1
    jmp     booth_4

booth_5
    sub.w   R15,x
    subc.w  R10,R13
booth_3
    rla.w   R15
    rlc.w   R10
    rra.w   R14
    jnc     booth_6
    cmp.w   #0xFFFF,R14
    jne     booth_3

booth_4
    pop.w   R15
    pop.w   R10
#endif
    xret
    end
